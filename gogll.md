# Gogll v3

[Copyright 2020 Marius Ackerman](License.txt)

This document contains a documented BNF specification for gogll 3. The formal
specification is contained the markdown code blocks, which are delimited by triple
backticks. Gogll treats all text outside the code blocks as whitespace.

This specification is an example of a gogll input specification. This version of gogll was generated by gogll from this specification.

Gogll treats this paragraph as whitespace. The following is the first code block
of this document.

```
package "github.com/goccmack/gogll/v3"
```
Every gogll v3 input specification starts with a package specification. The
package of this specification is `"github.com/goccmack/gogll"`.
The packages generated by gogll will have this package prefix.
For example: the [gogll parser generated from this grammar](parser/parser.go) has the following
imports:

	"github.com/goccmack/gogll/v3/lexer"
	"github.com/goccmack/gogll/v3/parser/bsr"
	"github.com/goccmack/gogll/v3/parser/slot"
	"github.com/goccmack/gogll/v3/parser/symbols"
	"github.com/goccmack/gogll/v3/v3/token"

The following grammar defines the input specification for gogll. `GoGLL` is syntax
start symbol and the root of the parse forest produced by the parser.
```
GoGLL : Package Rules ;

Package : "package" string_lit ;

Rules
    :   Rule            
    |   Rule Rules  
    ;

Rule : LexRule | SyntaxRule ;
```
The package specification is followed by one or more rules. Each rule can be a 
`LexRule` (token specification for the generated lexer) or a 
`SyntaxRule` (syntax specification for the generated parser).
The first `SyntaxRule` is taken as the syntax start symbol.

# Lexical Symbols
Gogll accepts UTF-8 input. In the following *character* refers to a Unicode 
character. Gogll and has the following built-in lexical symbols, 
which are used to specify tokens:

| Symbol | Matches |
|---|---|
| . | Any character 
| any | Any character in the string literal following `any`
| not | All characters execept those in the string literal following `not`
| char_lit | The specified character literal. See the lexical definition for `char_lit` below.
| letter | The Unicode category `L`
| upcase | Any uppercase letter
| lowcase | Any lowercase letter
| number | The Unicode category `N`
| bracketed expression | Grouped, optional or repeated alternates

`any`, `not`, `letter`, `upcase`, `lowcase` and `number` are the only reserved
words of gogll.

```
LexSymbol 
    :   "." | "any" string_lit | char_lit | LexBracket | "not" string_lit 
    |   UnicodeClass 
    |   UnicodeSet
    ;

UnicodeClass 
    :   "letter" | "upcase" | "lowcase" | "number" 
    ;

UnicodeSet : "'[" UnicodeSetSpec UnicodeSetSpecs "]'" ;
```
`UnicodeSet` is used to specify a set of Unicode character properties, e.g.:  
    `id_start : '[\p{L}\p{Nl}\p{Other_ID_Start}-\p{Pattern_Syntax}-\p{Pattern_White_Space}]' ;`

A character property may be included in a set, e.g.: `\p{L}`;   
or excluded from a set, e.g.: `-\p{Pattern_Syntax}`
```

UnicodeSetSpec : UnicodeCategory | UnicodeProperty ;

UnicodeSetSpecs 
    :   empty
    |   UnicodeSpecList
    ;

UnicodeSpecList
    :   PlusOrMinUnicodeSet
    |   PlusOrMinUnicodeSet UnicodeSpecList
    ;

PlusOrMinUnicodeSet
    :   UnicodeSetSpec
    |   "-" UnicodeSetSpec
    ;

```
`UnicodeCategory` and `UnicodeProperty` correspond with the matching range table variable
definitions in Go *package unicode*.

`UnicodeCategory` is the set of Unicode [*General Categories*](https://www.unicode.org/versions/Unicode14.0.0/ch04.pdf#G134153), as specified as variables in *package unicode*.
These categories may be used in the specification of a `UnicodeSet`.
```
UnicodeCategory 
    :   "\\p{Cc}"
    |   "\\p{Cf}"
    |   "\\p{Co}"
    |   "\\p{Cs}"
    |   "\\p{Digit}"
    |   "\\p{Nd}"
    |   "\\p{Letter}"
    |   "\\p{L}"
    |   "\\p{Lm}"
    |   "\\p{Lo}"
    |   "\\p{Lower}"
    |   "\\p{Ll}"
    |   "\\p{Mark}"
    |   "\\p{M}"
    |   "\\p{Mc}"
    |   "\\p{Me}"
    |   "\\p{Mn}"
    |   "\\p{Nl}"
    |   "\\p{No}"
    |   "\\p{Number}"
    |   "\\p{N}"
    |   "\\p{Other}"
    |   "\\p{C}"
    |   "\\p{Pc}"
    |   "\\p{Pd}"
    |   "\\p{Pe}"
    |   "\\p{Pf}"
    |   "\\p{Pi}"
    |   "\\p{Po}"
    |   "\\p{Ps}"
    |   "\\p{Punct}"
    |   "\\p{P}"
    |   "\\p{Sc}"
    |   "\\p{Sk}"
    |   "\\p{Sm}"
    |   "\\p{So}"
    |   "\\p{Space}"
    |   "\\p{Z}"
    |   "\\p{Symbol}"
    |   "\\p{S}"
    |   "\\p{Title}"
    |   "\\p{Lt}"
    |   "\\p{Upper}"
    |   "\\p{Lu}"
    |   "\\p{Zl}"
    |   "\\p{Zp}"
    |   "\\p{Zs}"
    ;
```
`UnicodeProperty` is the set of Unicode character properties supported in 
*package unicode*. They may be used in `UnicodeSet` specifications.
```
UnicodeProperty
    :   "\\p{ASCII_Hex_Digit}"
    |   "\\p{Bidi_Control}"
    |   "\\p{Dash}"
    |   "\\p{Deprecated}"
    |   "\\p{Diacritic}"
    |   "\\p{Extender}"
    |   "\\p{Hex_Digit}"
    |   "\\p{Hyphen}"
    |   "\\p{IDS_Binary_Operator}"
    |   "\\p{IDS_Trinary_Operator}"
    |   "\\p{Ideographic}"
    |   "\\p{Join_Control}"
    |   "\\p{Logical_Order_Exception}"
    |   "\\p{Noncharacter_Code_Point}"
    |   "\\p{Other_Alphabetic}"
    |   "\\p{Other_Default_Ignorable_Code_Point}"
    |   "\\p{Other_Grapheme_Extend}"
    |   "\\p{Other_ID_Continue}"
    |   "\\p{Other_ID_Start}"
    |   "\\p{Other_Lowercase}"
    |   "\\p{Other_Math}"
    |   "\\p{Other_Uppercase}"
    |   "\\p{Pattern_Syntax}"
    |   "\\p{Pattern_White_Space}"
    |   "\\p{Prepended_Concatenation_Mark}"
    |   "\\p{Quotation_Mark}"
    |   "\\p{Radical}"
    |   "\\p{Regional_Indicator}"
    |   "\\p{STerm}"
    |   "\\p{Sentence_Terminal}"
    |   "\\p{Soft_Dotted}"
    |   "\\p{Terminal_Punctuation}"
    |   "\\p{Unified_Ideograph}"
    |   "\\p{Variation_Selector}"
    |   "\\p{White_Space}"
    ;

char_lit : '\'' (not "'" | '\\' any "\\'nrt") '\'' ;
```
`char_lit` is a character literal enclosed in single quotes. A char literal may
be an escaped character:
|char_lit| Description |
|---|---|
`'\''` | single quote
`'\\'` | backslash 
`'\n'` | newline
`'\r'` | carriage return
`'\t'` | horizontal tab

Lexical symbols may be grouped and groups may be optional or repeated.
For example:
| Bracketed expression | Meaning
|---|---|
| `( ùú∂ )` | ùú∂ must occur once
| `[ ùú∂ ]` | ùú∂ may occur zero or one times
| `{ ùú∂ }` | ùú∂ may occur zero or more times
| `< ùú∂ >` | ùú∂ must occur one or more times

A bracketed expression may consist of one or more alternates:  
`ùú∂ ‚Üí Œ≤ | ùû¨ | ... | Œ¥`
Each alternate is a `RegExp` consisting of one or more `LexSymbols`

```
LexBracket : LexGroup | LexOptional | LexZeroOrMore | LexOneOrMore ;
LexGroup : "(" LexAlternates ")" ;
LexOptional : "[" LexAlternates "]" ;
LexZeroOrMore : "{" LexAlternates "}" ;
LexOneOrMore : "<" LexAlternates ">" ;
LexAlternates : RegExp | RegExp "|" LexAlternates ;
RegExp 
    : LexSymbol 
    | tokid
    | LexSymbol RegExp 
    | tokid RegExp 
    ;
```
A defined token, `tokid`, may be used as a lexical symbol in another token definition
as long as it introduces no direct or indirect recursion and it is defined before
it is referenced in another token definition. The compiler checks for recursion. 

# Lexical Rules
Gogll generates a lexer from the set of `string_lit` `SyntaxSymbol`s in the specification
(see **Syntax Rules** below) plus the set of `LexRule`s defined by the user.

```
LexRule
    : tokid ":" RegExp ";"
    | "!" tokid ":" RegExp ";"
    ;
```
The first alternate of `LexRule` is a normal token definition. The second alternate, which starts with `!` defines a token that will be suppressed by the lexer. An example of the use of suppressed tokens is to define code comments.
See [example](examples/comments/comments.md)
```
tokid : lowcase <letter|number|'_'> ; 
```
`tokid` is a token ID, which starts with a lower case letter, followed by one
or more `letter`, `number` or `'_'`.
The following production, which defines a token `string_lit`, 
is an example of a `LexRule`.
"
```
string_lit : '"' <not "\\\"" | '\\' any "\\\"nrt"> '"' ;
```
`string_lit` defines a (possibly empty) string literal. Note that `string_lit`
does not have the same set of escape characters as `char_lit` or `char_set`.

# Syntax Rules
Gogll uses the specified syntax rules to generate the parser.
```
SyntaxRule : nt ":" SyntaxAlternates ";"  ;
nt : upcase {letter|number|'_'} ;
```
`nt` is a `SyntaxRule` ID and stands for Non-terminal. An `nt` is distinguished 
from a `tokid` by its first character, which is upper case. `SyntaxRule` is 
an example of itself. The `nt` of the rule is `SyntaxRule`.

`SyntaxAlternates` is the `|`-separated list of valid alternates of a syntax rule.
```
SyntaxAlternates
    :   SyntaxAlternate                   
    |   SyntaxAlternate "|" SyntaxAlternates    
    ;
```

Each `SyntaxAlternate` is a sequence of `SyntaxSymbol`. An optional syntax rule may
have an alternate `empty`.
```
SyntaxAlternate
    :   SyntaxSymbols                     
    |   "empty"                     
    ;

SyntaxSymbols
    :   SyntaxSymbol                      
    |   SyntaxSymbol SyntaxSymbols              
    ;

SyntaxSymbol : nt | tokid | string_lit ;
```
A `string_lit` `SyntaxSymbol` may not contain whitespace characters.
